10 REM//INCOME VS EXPENSE BASIC PROGRAM FOR THE COCO PROGRAMED BY TAZROG//
20 REM//REQUIES COCO SDC COPYRIGHT 2024//
30 REM//V.5.4//
40 CLS:FORA=0TO8:READA$:POKE1024+A,VAL("&H"+A$):NEXTA:EXEC1024:DATAC6,1,96,BC,1F,2,7E,96,A3
50 CLEAR 500:Q=0:GT=0:DT=0:IT=0:QQ=0:DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
60 GOTO 3850
70 GOSUB 1590:GOSUB 1680:GOSUB 3210
80 CLS:T=150
90 PRINT@7,"INCOME VS EXPENSE":IF DA$=""THEN GOTO 110
100 PRINT@234,"DATE ";DA$:PRINT@262,"PRESS c TO CHANGE";
110 A$=INKEY$:IF A$=" "OR A$="M"THEN240
120 IFA$="C"THEN140
130 PRINT@293+32,"PRESS space TO START":GOTO 110
140 CLS:PRINT@7,"INCOME VS EXPENSE":PRINT@233,"";:PRINT"MONTH =##";MO$;:INPUTM$:IF M$=""THEN M$=MO$
150 IF M$="1"THENM$ ="01"ELSEIF M$="2"THEN M$="02"ELSEIFM$="3"THENM$ ="03"ELSE IF M$="6"THEN M$="06"
160 IF M$="7"THENM$ ="07"ELSEIFM$="8"THENM$="08"ELSEIFM$="9"THENM$="09" 
170 PRINT@265,"DAY =##";DL$;:INPUT D$:IF D$=""THEN D$=DL$:GOTO 210
180 IF D$="1"THEN D$="01"ELSE IF D$="2"THEN D$="02"ELSE IF D$="3"THEN D$="03"ELSE IF D$="6"THEN D$="06"
190 IF D$="7"THEN D$="07"ELSE IF D$="8"THEN D$="08"ELSE IF D$="9"THEN D$="09" 
200 IF VAL(D$)>31 THEN CLS:PRINT "INVALID DAY, CHOOSE AGAIN":FOR T=T TO 100:NEXT T:CLS:GOTO 170
210 DA$=M$+"/"+D$:CLS:PRINT@10,"DATE ";:PRINT DA$:PRINT"":T=0:INPUT"IS THIS CORRECT, c TO CHANGE";A$
220 IF A$="C"THEN GOTO 140 ELSE GOSUB 3140
230 CLS:GOTO 250
240 CLS:PRINT@268,"LOADING"
250 LO=40:CLS:PRINT"       --> DATE ";:PRINT DA$;:PRINT" <--":GT=IT-DT:A=0:A$=""
260 PRINT@LO-4,"***INCOME VS EXPENSE***";:LO=LO+32:PRINT@LO,"1- ENTER INCOME";:LO=LO+32:PRINT@LO,"2- ENTER EXPENSE";:LO=LO+32:PRINT@LO,"3- LIST/FIND INCOME";:LO=LO+32
270 PRINT@LO,"4- LIST/FIND EXPENSE";:LO=LO+32:PRINT@LO,"5- MONTHLY IVE";:LO=LO+32:PRINT@LO,"6- IVE GRAPH";:LO=LO+32:PRINT@LO,"7- CAT. INCOME";:LO=LO+32:PRINT@LO,"8- CAT. EXPENSE";:LO=LO+32
280 PRINT@LO,"9- SETTINGS":LO=LO+32:PRINT@LO-8,STRING$(32,"-"):PRINT@384,"INCOME  >>>";:PRINT@403,"";:PRINT USING"$$###,###.##";IT
290 PRINT@416,"EXPENSE >>>";:PRINT@435,"";:PRINT USING"$$###,###.##";DT
300 IF GT>0 THEN PRINT@448,"+$FLOW  >>>";ELSE PRINT@448,"-$FLOW  >>>";
310 PRINT@467,"";:PRINT USING"$$###,###.##";GT
320 A$=INKEY$
330 IF A$="1"OR A$="I"THEN 460
340 IF A$="2"OR A$="E"THEN 590
350 IF A$="3"THEN 5390
360 IF A$="4"THEN 5420
370 IF A$="5"THEN GOTO 5330
380 IF A$="6"OR A$="G"THEN 5360
390 IF A$="7"THEN CLS:GOTO 5220
400 IF A$="8"THEN CLS:GOTO 5190
410 IF A$="S"OR A$="9"THEN 2640
420 IF A$="H"THEN GOSUB 3780
430 IF A$="Q"THEN END
440 IF A$="R"THEN 240
450 GOTO 320
460 CLS:PRINT@6,"INCOME TRANSACTION":PRINT@70,"ENTER AMOUNT";:INPUT AM 
470 IF AM=0 GOTO 240
480 GOSUB 2170 
490 AM$=STR$(AM):CLS:PRINT@6,"INCOME TRANSACTION":PRINT@165,"TRANSACTION ENTERED":PRINT@225,DA$;:PRINT" ";:PRINT CI$;:PRINT USING"$$##,###.##";AM
500 PRINT"":PRINT"      PRESS cHANGE":PRINT"":PRINT"   PRESS aDD ANOTHER":INPUT" PRESS enter FOR MAIN SCREEN";A$
510 IF A$="C"THEN 460
520 OPEN"D",#1,"IN.DAT",30:FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$
530 IF A$="Q"THEN END
540 Q=LOF(1)
550 LSET DD$=DA$:LSET TT$=CI$:LSET CA$=AM$:PUT#1,1+Q:Q=Q+1:CLOSE#1
560 IT=IT+AM:GT=GT+IT
570 IF A$="A"THEN 460
580 GOTO 5040
590 CLS:PRINT@6,"EXPENSE TRANSACTION":PRINT@70,"ENTER AMOUNT";:INPUT IM 
600 IF IM=0 GOTO 5040
610 GOSUB 2170
620 IM$=STR$(IM)
630 CLS: PRINT@6,"EXPENSE TRANSACTION":PRINT@165, "TRANSACTION ENTERED":PRINT@225,DA$;:PRINT" ";:PRINT CI$;:PRINT USING "$$##,###.##";IM
640 PRINT"":PRINT"      PRESS cHANGE":PRINT"":PRINT"   PRESS aDD ANOTHER":INPUT " PRESS enter FOR MAIN SCREEN";A$
650 IF A$="C"THEN 590
660 OPEN"D",#1,"OUT.DAT",30:FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
670 K=LOF(1)
680 LSET PD$=DA$:LSET PT$=CI$:LSET PA$=IM$
690 PUT#1,1+K:K=K+1:CLOSE#1
700 L=VAL(A$):
710 DT=DT-(IM*-1):GT=GT+DT
720 IF A$="A"THEN 590
730 GOTO 5040
740 OPEN"D",#1,"IN.DAT",30:K$="INCOME":L$=""
750 CLS:IF LOF(1)<1THEN PRINT"NO DATA TO SHOW":CLOSE#1:GOTO250
760 S$="":LO=31:CLS:INPUT"SEARCH dATE cAT aMT mENU:";S$:IF S$="M" THEN CLOSE#1:GOTO 5050
770 CLS:IF S$="D" THEN GOTO 790 ELSEIF S$="C" THEN 800 ELSEIFS$="A" THEN 810 ELSEIF S$="Y" THEN JJ$="":GOTO 840
780 PRINT "INVALID CHOICE, RESELECT":GOTO 760
790 PRINT"ENTER DATE SEARCH CRITERA":INPUTJJ$:L=LEN(JJ$):LT=0;GOTO 830
800 PRINT"ENTER CAT SEARCH CRITERA":INPUTJJ$:L=LEN(JJ$):LT=0:GOTO 840
810 PRINT"ENTER AMOUNT SEARCH CRITERA":INPUTJJ$:L=LEN(JJ$):LT=0GOTO 840
820 GOTO 760
830 IF JJ$="1"THENJJ$ ="01"ELSEIF JJ$="2"THEN JJ$="02"ELSEIFJJ$="3"THENJJ$ ="03"ELSE IF JJ$="6"THEN JJ$="06"ELSEIFJJ$="7"THENJJ$="07"ELSEIFJJ$="8"THENJJ$="08"ELSEIFJJ$="9"THENJJ$="09" 
840 FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$:LT=0
850 G=LOF(1):CLS
860 IF S$="C" THEN P$=" CAT- " ELSEIF S$="D" THEN P$=" DATE- " ELSE IF S$="A" THEN P$=" AMT- " ELSE P$=" YEAR "
870 FOR E=1 TO G:GET#1,E:IF VAL(CA$)<1 THEN 1000
880 PRINT @0,"#    DATE   INCOME      AMOUNT"
890 PRINT@480,"SEARCHING";P$;JJ$;:PRINT @502,"";:PRINT USING"###";(E/G)*100;:PRINT"%";
900 IF S$="Y" THEN 950
910 IF S$="D" AND JJ$=LEFT$(DD$,L)THEN950
920 IF S$="C" AND JJ$=LEFT$(TT$,L)THEN 950 
930 IF S$="A" AND VAL(JJ$)=VAL(LEFT$(CA$,L+1))THEN950
940 GOTO 1000
950 K$="?-"+JJ$:LT=LT+VAL(CA$):PRINT@LO,E;
960 PRINT@LO+6,DD$;:PRINT@LO+14,TT$;:PRINT@LO+21,"";:PRINT USING"$$##,###.##";VAL(CA$)
970 LO=LO+32:L$=""
980 IFLO>448THEN PRINT@480,"        enter-CONT -mENU";:INPUTL$:IFL$="M"THEN CLOSE#1:GOTO5050
990 IFLO>448THEN CLS:PRINT"#    DATE     INCOME      AMOUNT":LO=31
1000 NEXT E 
1010 PRINT@448,K$:PRINT@462,"";:PRINT USING"total$$###,###.##";LT
1020 PRINT@480,"-mENU -rEDO -aDD -DEL##  ";:LT=0:INPUTA$:IFA$="R"THEN 750
1030 IFA$=" "THEN CLOSE#1:GOTO1110
1040 IFA$="Q"THEN CLOSE#1:EXP
1050 IFA$="M"THEN CLOSE#1:GOTO5050
1060 IFA$="X"THEN CLOSE#1:GOTO740
1070 IFA$="H"THEN GOSUB3780
1080 IFA$="A" THEN CLOSE#1:GOTO460
1090 IFA$="5"THEN CLOSE#1:GOTO1770
1100 IFVAL(A$)>0THEN1140 ELSE 1020
1110 GOSUB1590:GOTO5040
1120 CLOSE#1
1130 GOTO1190
1140 CLS: GET#1,VAL(A$)
1150 PRINT @256,DD$:PRINT @264,TT$:PRINT@274,"";:PRINT USING "$$##,###.00";VAL(CA$)
1160 INPUT"DELETE Y/N";L$:IF L$="Y"THEN 1170 ELSE CLOSE#1:GOTO 1190
1170 LSET DD$="":LSET TT$="":LSET CA$="":PUT#1,VAL(A$)
1180 CLOSE#1
1190 GOTO 5040
1200 GOTO740
1210 OPEN"D",#1,"OUT.DAT",30:S$="":LT=0
1220 CLS:IF LOF(1)<1THEN PRINT"NO DATA TO SHOW":CLOSE#1:GOTO250
1230 LO=31:CLS:INPUT"SEARCH dATE cAT aMT mENU:";S$:L$="":IF S$="M" THEN CLOSE#1:GOTO 5050
1240 CLS:IF S$="D" THEN GOTO 1260 ELSEIF S$="C" THEN 1270 ELSEIFS$="A" THEN 1280 ELSEIF S$="Y"THEN JJ$="":GOTO 1310
1245 IF A$="M" GOTO 5050
1250 PRINT "INVALID CHOICE, RESELECT":GOTO 760
1260 PRINT"ENTER DATE SEARCH CRITERA":INPUTJJ$:L=LEN(JJ$):LT=0:GOTO 1300
1270 PRINT"ENTER CAT SEARCH CRITERA":INPUTJJ$:L=LEN(JJ$):LT=0:GOTO 1310
1280 PRINT"ENTER AMOUNT SEARCH CRITERA":INPUTJJ$:L=LEN(JJ$):LT=0:GOTO 1310
1290 GOTO 1230
1300 IF JJ$="1"THENJJ$ ="01"ELSEIF JJ$="2"THEN JJ$="02"ELSEIFJJ$="3"THENJJ$ ="03"ELSE IF JJ$="6"THEN JJ$="06"ELSEIFJJ$="7"THENJJ$="07"ELSEIFJJ$="8"THENJJ$="08"ELSEIFJJ$="9"THENJJ$="09" 
1310 CLS
1320 IF S$="C" THEN P$=" CAT- " ELSEIF S$="D" THEN P$=" DATE- " ELSE IF S$="A" THEN P$=" AMT- " ELSE P$=" YEAR "
1330 FIELD#1,10 AS PD$,10 AS PT$,10 AS PA$:K$="EXPENSE":G=LOF(1)
1340 FOR E=1 TO G:GET#1,E:IF VAL(PA$)<1 THEN 1450
1350 PRINT @0,"#    DATE   EXPENSE      AMOUNT"
1360 PRINT@480,"SEARCHING";P$;JJ$;:PRINT @502,"";:PRINT USING"###";(E/G)*100;:PRINT"%";
1370 IF S$="Y" THEN1420
1380 IF S$="D" AND JJ$=LEFT$(PD$,L)THEN1420
1390 IF S$="C" AND JJ$=LEFT$(PT$,L)THEN1420
1400 IF S$="A" AND VAL(JJ$)=VAL(LEFT$(PA$,L+1))THEN1420
1410 GOTO 1450
1420 K$="?-"+JJ$:LT=LT+VAL(PA$):PRINT@LO,E;:PRINT@LO+6,PD$;:PRINT@LO+14,PT$;:PRINT@LO+21,"";:PRINT USING"$$##,###.##";VAL(PA$)
1430 LO=LO+32:IF LO>448 THEN PRINT@480,"        enter-CONT mENU";:INPUT L$:IFL$="M"THEN CLOSE#1:GOTO5050
1440 IFLO>448THEN CLS:PRINT"#    DATE    EXPENSE     AMOUNT":LO=31
1450 NEXT E 
1460 PRINT@448,K$:PRINT @462,"";:PRINT USING "total$$###,###.##";LT
1470 PRINT@480,"-mENU -rEDO -aDD DEL##  ";:INPUT A$:IF A$="R" THEN 1220
1480 IFA$="Q"THEN CLOSE#1:EXP
1490 IFA$="M"THEN CLOSE#1:GOTO5050
1500 IFA$="A"THEN CLOSE#1:GOTO590
1510 IF VAL(A$)>0 THEN1520 ELSE CLOSE#1:GOTO 260
1520 IF VAL(A$)>LOF(1) THEN GOTO 1470
1530 CLS:GET#1,VAL(A$)
1540 PRINT@256,PD$:PRINT @264,PT$:PRINT@274,"";:PRINT USING "$$##,###.##";VAL(PA$):INPUT"DELETE Y/N";L$:IF L$="Y"THEN 1550 ELSE GOTO 1220
1550 LSET PD$="":LSET PT$="":LSET PA$="":PUT#1,VAL(A$)
1560 CLOSE#1
1570 GOTO 5040
1580 GOTO1210
1590 OPEN"D",#1,"IN.DAT",30:GT=0:IT=0
1600 IF LOF(1)<1THEN CLOSE#1:RETURN
1610 FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$:
1620 G=LOF(1)
1630 FORE=1TOG
1640 GET#1,E
1650 IT=IT+VAL(CA$)
1660 NEXT E
1670 CLOSE#1:RETURN
1680 OPEN"D",#1,"OUT.DAT",30:GT=0:DT=0
1690 IF LOF(1) <1 THEN CLOSE #1:RETURN
1700 FIELD#1,10 AS DD$,10 AS TD$,10 AS CD$
1710 G=LOF(1)
1720 FOR E=1TOLOF(1)
1730 GET#1,E 
1740 DT=DT+VAL(CD$)
1750 NEXT E:CLOSE#1
1760 RETURN
1770 CLS:PRINT@267,"CALCULATING";
1780 FOR X=1TO12:MI(X)=0:MD(X)=0:NEXT X
1790 OPEN"D",#1,"IN.DAT",30
1800 IF LOF(1)<1 THEN CLOSE #1:GOTO 1880
1810 FIELD#1,10 AS MD$,10 AS MC$,10 AS MZ$
1820 FOR E=1TOLOF(1)
1830 GET#1,E
1840 MT=VAL(LEFT$(MD$,2))
1850 GOSUB 2100
1860 NEXT E
1870 CLOSE #1
1880 OPEN"D",#1,"OUT.DAT",30:MU=0:MG=0:MP=0
1890 IF LOF(1)<1 THEN CLOSE #1:A$="R":GOTO 5040
1900 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
1910 G=LOF(1)
1920 FOR E=1TOG
1930 PRINT@302,"";:PRINT USING"###";(E/G)*100;:PRINT"%"
1940 GET#1,E:IQ=VAL(LEFT$(ID$,2))
1950 GOSUB 2130
1960 NEXTE 
1970 CLOSE#1
1980 CLS(0):LO=32
1990 PRINT"m";:PRINT@4,"income";:PRINT@14,"expense";:PRINT@27,"diff";
2000 FOR X= 1TO12
2010 PRINT@LO,RIGHT$(STR$(X),1);:PRINT@LO+1,"";:PRINT USING"#######.##";MI(X);:MU=MU+MI(X)
2020 PRINT@LO+12,"";:PRINT USING"#######.##";MD(X);:MG=MG+MD(X):PRINT@LO+23,"";:PRINT USING"######.##";MI(X)-MD(X);:MP=MP+(MI(X)-MD(X))
2030 LO=LO+32:NEXT X
2040 PRINT@LO+13,"totals";:LO=LO+32:PRINT@LO,"":PRINT @ LO+2,"";:PRINT USING"######.##";MU
2050 PRINT@LO+13,"";:PRINT USING"######.##";MG:PRINT@LO+23,"";:PRINT USING"######.##";MP;
2060 A$=INKEY$
2070 IFA$=" " OR A$="M"THEN GOTO 5050
2080 IFA$="G"THEN 5360
2090 GOTO2060
2100 FORX=1TO12
2110 IFX=MT THEN MI(MT)=MI(MT)+VAL(MZ$)
2120 NEXTX:RETURN
2130 FORX=1TO12
2140 IFX=IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
2150 NEXTX
2160 RETURN
2170 REM//CATEGORIES//
2180 CLS:PRINT"          CATEGORIES"
2190 OPEN"D",#1,"CAT.DAT",10:QQ=0:LO=32
2200 IF LOF(1)<1THEN QQ=99:CLOSE#1:GOTO 2390
2210 FIELD #1,10 AS CT$
2220 FORE=1TOLOF(1)
2230 GET#1,E 
2240 PRINT@LO,E;:PRINT"-";
2250 PRINT CT$;
2260 CG$(E)=CT$
2270 LO=LO+32/2
2280 NEXTE
2290 PRINT"":PRINT@15*32,"";
2300 CLOSE#1
2310 IFA=6 THEN INPUT"99 TO ADD OR ## TO EDIT";QQ
2320 IFA=6 AND QQ=99 THEN 2390 
2330 IFA=6 AND QQ<1 THEN A$="R":GOTO 5040
2340 IFA=6 THEN 2380
2350 INPUT"ENTER CAT.# OR 99 TO ADD";L:IF L=99 THEN QQ=99:GOTO 2390
2360 CI$=CG$(L):A$="":Z=0
2370 A$="R":RETURN
2380 PRINT"EDIT CATEGORIES"
2390 OPEN"D",#1,"CAT.DAT",10
2400 FIELD#1,10 AS CA$
2410 IFB>28 THEN 2590
2420 IFQQ=99 THEN B=LOF(1)+1:GOTO 2440
2430 IFQQ<99 AND QQ>0 THEN B=QQ
2440 IFB>28 THEN CLOSE#1:PRINT"MAX CAT":FOR X=1TO1500:NEXT X:GOTO 5040
2450 INPUT"NEW CAT NAME";CD$:IF QQ=99 GOTO 2470
2451 IF CD$="" THEN CLS:PRINT "THIS WILL DELETE THE CAT AND":PRINT"RELATED RECORDS":INPUT "Y/N";Z$:IF Z$="Y" GOTO 2460 ELSE CLOSE#1:GOTO2640
2460 GET #1,B:TM$=CA$:IF LEFT$(TM$,3)="   " GOTO 2470
2465 CLS:PRINT@7,"THIS WILL REPLACE":PRINT@49-(LEN(CA$)/2),"";:PRINT CA$:PRINT@78,"WITH":PRINT@112-(LEN(CD$)/2),"";CD$:PRINT @130,"AND UPDATE ALL TRANSACTIONS":PRINT @238,"";:INPUT"Y/N";X$:IF X$="Y" THEN GOTO 2470 ELSE CLOSE#1
2467 GOTO 2640
2470 LSET CA$=CD$
2480 PUT#1,B
2490 CLOSE#1:IF QQ=99 GOTO 2620
2491 IF LEFT$(TM$,3)="   " GOTO 2640
2495 CLS
2500 OPEN"D",#1,"OUT.DAT",30:XX=00
2510 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
2520 FOR ZZ=1 TO LOF(1):GET#1,ZZ
2530 ND$=ID$:NZ$=IZ$
2531 IF CD$=""THEN ND$="":NZ$=""
2540 IF TM$=IC$ THEN LSET ID$=ND$:LSET IC$=CD$:LSET IZ$=NZ$:PUT #1,ZZ:XX=XX+1
2545 PRINT @235,"UPDATING":PRINT @262, "UPDATED";XX;:PRINT " RECORDS"
2550 NEXTZZ: CLOSE#1
2560 OPEN"D",#1,"IN.DAT",30
2570 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
2580 FOR ZZ=1 TO LOF(1):GET#1,ZZ
2590 ND$=ID$:NZ$=IZ$
2591 IF CD$=""THEN ND$="":NZ$=""
2600 IF TM$=IC$ THEN LSET ID$=ND$:LSET IC$=CD$:LSET IZ$=NZ$:PUT #1,ZZ:XX=XX+1
2605 PRINT @235,"UPDATING":PRINT @262, "UPDATED";XX;:PRINT " RECORDS"
2610 NEXTZZ: CLOSE#1:GOSUB 5620:PRINT @296,"UPDATE COMPLETE";:INPUTZ
2620 IFA<6THEN GOTO 2170
2630 A$="R":GOTO 5040
2640 REM //SETTING SCREEN//
2650 CLS:A$=""
2660 PRINT"SETTINGS":PRINT"":PRINT"1- EDIT CATEGORIES":PRINT"2- CLEAR INCOME DATA":PRINT"3- CLEAR EXPENSE DATA":PRINT"4- CLEAR CATEGORIES DATA":PRINT"5- CHANGE DATE"
2670 PRINT"6- BACKUP":PRINT"7- CLOSE OUT YEAR":PRINT"8- CLEAN DATA":PRINT"[M]- MAIN MENU":PRINT"H- HELP"
2680 A$=INKEY$
2690 IFA$="1"THEN A=6:GOSUB 2170:GOTO 2640
2700 IFA$="2"THEN PRINT"CLEARED INCOME DATA":INPUT"ARE YOU SURE <Y/N>?";L$: IF L$ ="Y"THEN GOTO 2790 ELSE GOTO 240
2710 IFA$="3"THEN PRINT"CLEARED EXPENSE DATA":INPUT"ARE YOU SURE <Y/N>?";L$: IF L$ ="Y"THEN GOTO 2800 ELSE GOTO 240
2720 IFA$="4"THEN PRINT"CLEARED CATEGORIES":INPUT"ARE YOU SURE <Y/N>?";L$: IF L$ ="Y"THEN GOTO 2810 ELSE GOTO 240
2730 IFA$="5"THEN PRINT"NEW DATE":GOTO 140
2740 IFA$="6"THEN GOTO2830
2750 IFA$="7"THEN GOTO2930
2755 IFA$="8"THEN GOSUB5450:CLS:GOTO 2640
2760 IFA$="M" OR A$=" "THEN GOTO5040
2770 IFA$="H"GOSUB3780:GOTO2640
2780 GOTO2680
2790 KILL"IN/DAT":IT=0:GOTO250
2800 KILL"OUT/DAT":DT=0:GOTO250
2810 KILL"CAT/DAT":GOTO250
2820 GOTO5040
2830 REM//BACKUP// 
2840 CLS:PRINT"":A$=""
2850 CLEAR 500
2860 PRINT"MAKE SURE IVE IS IN DRIVE O.":PRINT "DRIVE 1 WILL BE ERASED.":PRINT"THIS REQUIRES DRIVEWIRE.":PRINT"cANCEL": INPUT"ANY OTHER KEY TO EXEC";A$:IF A$="C" THEN 2640
2861 OPEN"D",#1,"YEAR.DAT",2
2862 FIELD#1,2 AS YR$:GET#1,1
2863 A$=YR$
2865 CLOSE#1
2870 CLS:PRINT "FORMATING DRIVE 1":DSKINI1:CLS:PRINT"CONDUCTING BACKUP":BACKUP 0TO1:RENAME DRIVE1,"IVEBU"+A$+" "
2900 PRINT"BACKUP COMPLETE":PRINT"BACKUP DISK DRIVE NAMED IVEBU"+A$
2910 FORX=1TO1500:NEXTX
2920 GOTO5040
2930 REM //YEAR CLOSE OUT//
2940 CLEAR 500:CLS
2950 PRINT"THIS WILL CLOSEOUT THE YEAR,":PRINT"AND ARCHIVE THE DATA ON DISK,":PRINT"AND CREATE A NEW YEAR."
2960 PRINT"ARE YOU SURE?":INPUT"TYPE YES AND ENTER TO CONT.";B$: IF B$="YES"THEN GOTO 2970 ELSE GOTO5040
2970 CLS:PRINT"YEAR CLOSE OUT AND NEW YEAR DISK."
2971 OPEN"D",#1,"YEAR.DAT",2
2972 FIELD#1,2 AS YR$:IF LOF(1)<1 THEN 2980 
2973 GET #1,1:A$=YR$
2974 GOTO 3000
2980 INPUT"WHAT YEAR(##) IS THIS FOR ";A$: 
2981 IF LEN(A$)<2 OR LEN(A$)>2 THEN PRINT"USE ONLY 2 DIGITS FOR YEAR.":GOTO 2980
3000 PRINT"THIS WILL CLOSE OUT YEAR ";A$:PRINT"c TO CANCEL":INPUT"ANY OTHER KEY TO EXECUTE.";B$: IF B$="C"THEN 2640
3003 LSET YR$=A$
3004 PUT#1,1
3010 CLS:DRIVE1,"IVE"+A$+"-C.DSK",NEW:PRINT"FORMATING DRIVE":DSKINI1
3020 PRINT"DRIVE FORMATTED.":PRINT"TRANSFERING DATA TO DISK."
3030 BACKUP 0 TO 1
3040 PRINT"CLOSEOUT COMPLETE.":PRINT"ARCHIVE DISK NAMED IVE"+A$+"-C.DSK":CLOSE#1
3050 CLEAR 500
3060 CLOSE#1:OPEN"D",#1,"YEAR.DAT",2
3061 FIELD#1,2 AS YR$
3062 IF LOF(1)<1 THEN INPUT "WHAT IS THE CURRENT YEAR ##";A$ ELSE 3070
3065 IF LEN(A$)>2 OR LEN(A$)<1 THEN PRINT "USE ONLY 2 DIGITS FOR YEAR.":GOTO 3062
3067 LSET YR$=A$:PUT#1,1
3069 GOTO 3080
3070 GET #1,1
3065 A=VAL(YR$):A=A+1:A$=RIGHT$(STR$(A),2)
3080 PRINT"CREATING NEW YEAR- ";A$
3090 KILL"IN/DAT":KILL"OUT/DAT":KILL"DATE/DAT":DA$="01/01":GOSUB 3140
3100 DRIVE1,"IVE"+A$+".DSK",NEW:PRINT"FORMATING DRIVE.":DSKINI1:BACKUP 0 TO 1
3110 PRINT"CLOSEOUT COMPLETE."
3120 FOR X=1TO1500:NEXT:CLOSE#1
3130 EXP
3140 CLOSE#1:OPEN"D",#1,"DATE.DAT",5
3150 FIELD#1,5 AS DJ$
3160 B=LOF(1)
3170 LSET DJ$=DA$
3180 PUT#1,1
3190 CLOSE#1
3200 RETURN
3210 OPEN"D",#1,"DATE.DAT",5
3220 B=LOF(1)
3230 IFB<1THENCLOSE#1:CLS:PRINT"CREATE NEW YEAR DISK":GOTO 3050
3240 FIELD#1,5 AS DJ$
3250 GET#1,1
3260 DA$=DJ$
3270 MO$=LEFT$(DJ$,2):DL$=RIGHT$(DJ$,2)
3280 CLOSE#1
3290 RETURN
3300 INPUT"mENU OR MONTH(1-13)";A$:WW$="":K$="year":CLS:IFA$="M"THEN5050
3310 IFA$="1"THEN A$ ="01"ELSE IF A$="2"THEN A$="02"ELSE IF A$="3"THEN A$ ="03"ELSE IF A$="6"THEN A$="06"
3320 IFA$="7"THEN A$="07"ELSE IF A$="8"THEN A$="08"ELSE IF A$="9"THEN A$="09" 
3330 IFA$="01"THENK$="jan"ELSEIFA$="02"THENK$="feb"ELSEIF A$="03"THENK$="mar"ELSEIF A$="04"THEN K$="apr"ELSEIFA$="05"THENK$="may"ELSEIFA$="06"THENK$="jun"ELSEIFA$="07"THENK$="jul"ELSEIFA$="08"THENK$="aug"
3340 IFA$="09"THENK$="sep"ELSEIF A$="10"THENK$="oct"ELSEIFA$="11"THENK$="nov"ELSEIFA$="12"THENK$="dec"
3350 OPEN"D",#1,"CAT.DAT",10:WW$=A$:A$="":S=0:A$="":IF WW$="13"THEN WW$=""
3360 IFLOF(1)<1 THEN CLOSE#1:A$="R":GOTO 5040
3370 FIELD#1,10 AS CT$
3380 G=LOF(1)
3390 FORE=1TOG
3400 GET#1,E 
3410 CG$(E)=CT$
3420 NEXTE
3430 CLOSE#1
3440 FORX=1TOG 
3450 OT(X)=0
3460 NEXTX
3470 IP=0
3480 OPEN"D",#1,"OUT.DAT",30
3490 IFLOF(1)<1THEN CLOSE #1:A$="R":GOTO 5040
3500 FIELD#1,10 AS ID$,10 AS IC$,10 AS IZ$
3510 P=LOF(1)
3520 FORE=1TOP
3530 PRINT@267,"CALCULATING":PRINT@302,"";:PRINT USING"###";(E/P)*100;:PRINT"%"
3540 GET#1,E:IF WW$=""THEN GOTO 3570
3550 IFLEFT$(ID$,2)=WW$ THEN 3570
3560 GOTO3590
3570 IL$=IC$
3580 IP=VAL(IZ$)
3590 FORW=1TOG:IFWW$=""GOTO 3620
3600 IFLEFT$(ID$,2)=WW$ THEN 3620
3610 GOTO 3630
3620 IFIL$=CG$(W) THEN OT(W)=OT(W)+IP
3630 NEXTW,E
3640 CLOSE#1
3650 CLS(0):LO=32
3660 PRINT@0,"category";:PRINT @15,K$;:PRINT @ 24,"expense";
3670 FORX=1TOG
3680 IFOT(X)<1THEN GOTO 3730
3690 PRINT@LO,CG$(X);:PRINT STRING$(6," ");:
3700 PRINT@LO+18,"";:PRINT USING"$$###,###.##";OT(X)
3710 LO=LO+32
3720 IFLO>448THEN PRINT@480,"PRESS enter TO CONT.";:INPUT L:CLS(0):PRINT@0,"category";:PRINT@15,K$;:PRINT@24,"expense";:LO=32
3730 NEXTX
3740 PRINT@480,"PRESS space TO CONT";
3750 A$=INKEY$
3760 IF A$=" " THEN CLS:GOTO 3300
3770 GOTO 3750
3780 REM//HELP SCREEN//
3790 CLS:A$="":PRINT"           HELP":PRINT"SHORTCUTS":PRINT"[H] KEY-HELP SCREEN":PRINT"[S] KEY-SETTINGS":PRINT"[M] KEY-MONTHLY #-LIST SCREENS":PRINT"[G] KEY-GRAPHS ON MONTHLY"
3800 PRINT"[Q] KEY-QUIT":PRINT"[SPACE] KEY-MAIN MENU":PRINT"[ENTER] KEY- MAIN MENU"
3810 A$=INKEY$
3820 IFA$=" " OR A$="M" THEN A$="R":RETURN
3830 IFA$="Q"THEN END
3840 GOTO 3810
3850 REM //SPLASH SCREEN//
3860 CLS:C=143:A$="":PRINT@332,"FOR THE":PRINT@358,"TANDY COLOR COMPUTER":PRINT@72,"INCOME VS EXPENSE":PRINT@171,"CREATED BY"
3870 PRINT@205,"TAZROG":PRINT@238,"2024":PRINT@460,"LOADING"
3880 GOTO 70
3890 RETURN
3900 INPUT"mENU OR MONTH(1-13)";A$:WW$="":K$="year":CLS:IFA$="M"THEN5040
3910 IFA$="1"THEN A$ ="01"ELSE IF A$="2"THEN A$="02"ELSE IF A$="3"THEN A$ ="03"ELSE IF A$="6"THEN A$="06"
3920 IFA$="7"THEN A$="07"ELSE IF A$="8"THEN A$="08"ELSE IF A$="9"THEN A$="09" 
3930 IFA$="01"THENK$="jan"ELSEIFA$="02"THENK$="feb"ELSEIF A$="03"THENK$="mar"ELSEIF A$="04"THEN K$="apr"ELSEIFA$="05"THENK$="may"ELSEIFA$="06"THENK$="jun"ELSEIFA$="07"THENK$="jul"ELSEIFA$="08"THENK$="aug"
3940 IFA$="09"THENK$="sep"ELSEIF A$="10"THENK$="oct"ELSEIFA$="11"THENK$="nov"ELSEIFA$="12"THENK$="dec"
3950 OPEN"D",#1,"CAT.DAT",10:WW$=A$:A$="":S=0:IF WW$="13"THEN WW$=""
3960 IF LOF(1)<1 THEN CLOSE#1:A$="R":GOTO 5040
3970 FIELD#1,10 AS CT$
3980 G=LOF(1)
3990 FORE=1TOG
4000 GET#1,E 
4010 CG$(E)=CT$
4020 NEXTE
4030 CLOSE#1
4040 FOR X=1TOG 
4050 OT(X)=0
4060 NEXTX
4070 IP=0
4080 OPEN "D",#1,"IN.DAT",30
4090 IF LOF(1) <1 THEN CLOSE #1:A$="R":GOTO 5040
4100 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
4110 P=LOF(1)
4120 FOR E=1 TO P
4130 PRINT@267,"CALCULATING":PRINT@302,"";:PRINT USING "###";(E/P)*100;:PRINT"%"
4140 GET#1,E :IF WW$=""THEN GOTO 4170
4150 IFLEFT$(ID$,2)=WW$ THEN 4170
4160 GOTO 4190
4170 IL$=IC$
4180 IP= VAL(IZ$)
4190 FOR W=1 TO G:IF WW$="" GOTO 4220
4200 IF LEFT$(ID$,2)=WW$ THEN 4220
4210 GOTO 4230
4220 IFIL$=CG$(W) THEN OT(W)=OT(W)+IP
4230 NEXTW,E
4240 CLOSE#1
4250 CLS(0):LO=32
4260 PRINT@0,"category";:PRINT @15,K$;:PRINT @ 24,"income";
4270 FORX=1 TO G
4280 IF OT(X)<1 THEN GOTO 4330
4290 PRINT@LO,CG$(X);:PRINT STRING$(6," ");:
4300 PRINT@ LO+18,"";:PRINT USING"$$###,###.##";OT(X)
4310 LO=LO+32
4320 IFLO>448 THEN PRINT@480,"PRESS enter TO CONT.";:INPUT L:CLS(0):PRINT@0,"category";:PRINT@15,K$;:PRINT@24,"income";:LO=32
4330 NEXT X
4340 PRINT@480,"PRESS space TO CONT";
4350 A$=INKEY$
4360 IF A$=" " THEN CLS:GOTO 3300
4370 GOTO 4350
4380 REM GRAPHS
4390 CLS:HT=0:HM=0:HH=0:A$=""
4400 FORX=1TO12:MI(X)=0:MD(X)=0:NEXT X
4410 PRINT@268,"LOADING";
4420 FORX=1 TO 12:MI(X)=0:MD(X)=0
4430 OPEN"D",#1,"IN.DAT",30
4440 IFLOF(1) <1 THEN CLOSE #1:GOTO 4520
4450 FIELD #1,10 AS MD$,10 AS MC$,10 AS MZ$
4460 G=LOF(1)
4470 FORE=1 TO G
4480 GET#1,E
4490 MT=VAL(LEFT$(MD$,2))
4500 GOSUB4940
4510 NEXTE
4520 CLOSE#1
4530 OPEN"D",#1,"OUT.DAT",30:MU=0:MG=0:MP=0
4540 IFLOF(1)<1 THEN CLOSE #1:A$="R":GOTO 5040
4550 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
4560 G=LOF(1)
4570 PRINT@267,"CALCULATING"
4580 FORE=1TOG
4590 GET#1,E 
4600 IQ=VAL(LEFT$(ID$,2))
4610 PRINT@302,"";:PRINT USING"###";(E/G)*100;:PRINT"%"
4620 GOSUB 4990
4630 NEXTE 
4640 CLOSE#1
4650 IF HT<HH THEN HT=HH
4660 IF HT<>INT(HT)THEN HT=INT(HT)+1
4670 HM=HT/2:IFHM<>INT(HM)THEN HM=INT(HM)+1
4680 CLS(0):Y$=STRING$(1,126):PRINT @7,"income";:SET(13,0,1):SET(13,1,1):SET(26,0,1):SET(26,1,1):PRINT@16,"vs";:PRINT@21,"expense";:SET(41,0,4):SET(41,1,4):SET(56,0,4):SET(56,1,4)
4690 PRINT@419,Y$;:PRINT@433,Y$;:PRINT@447,Y$;
4700 PRINT@450,STR$(0):PRINT@448+17-LEN(STR$(HM))/2,HM:PRINT@480-LEN(STR$(HT)),STR$(HT);
4710 FOR X= 1 TO 12:IF X=1 THEN X$="jan"ELSE IF X=2 THEN X$="feb"ELSE IF X=3 THEN X$="mar"ELSE IF X=4 THEN X$="apr"ELSE IF X=5 THEN X$="may"ELSE IF X=6 THEN X$="jun"ELSE IF X=7 THEN X$="jul"ELSE IF X=8 THEN X$="aug"
4720 IF X=9 THEN X$="sep"ELSE IF X=10 THEN X$="oct"ELSE IF X=11 THEN X$="nov"ELSE IF X=12 THEN X$="dec"
4730 PRINT@32*X,X$;
4740 IF MI(X)>MD(X) THEN BB=MI(X):C=1
4750 IF MD(X)>=MI(X) THEN BB=MD(X):C=4
4760 IF BB<>INT(BB)THEN BB=INT(BB)+1
4770 FORG=6TO(BB/(HT/55)+8)
4780 IFBB<1THEN4800
4790 SET (G,2*X,C)
4800 NEXTG 
4810 IFMI(X)<MD(X) THEN BD=MI(X):C=1: 
4820 IFMD(X)<=MI(X) THEN BD=MD(X):C=4
4830 IFBD<>INT(BD) THEN BD=INT(BD)+1
4835 IF INT((BD/(HT/55)+8))=INT((BB/(HT/55)+8)) THEN C=3
4840 FORH=6TO(BD/(HT/55)+8)
4850 IFBD<1GOTO 4870
4860 SET(H,2*X,C)
4870 NEXTH,X
4880 A$=INKEY$
4890 IFA$=" " OR A$="M"THEN GOTO 5050
4910 IFA$="S"THEN GOTO 2640
4920 IFA$="H"GOSUB 3780:GOTO 5040
4930 GOTO 4880
4940 FORX=1TO12
4950 IFX=MT THEN MI(MT)=MI(MT)+VAL(MZ$)
4960 IFMI(X)>HT THEN HT=MI(X)
4970 NEXTX
4980 RETURN
4990 FORX=1TO12
5000 IFX=IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
5010 IFMD(X)>HH THEN HH=MD(X)
5020 NEXTX 
5030 RETURN
5040 REM//MEMORY MANAGAMENT//
5050 CLEAR 500:CLS:PRINT@268,"LOADING"
5060 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30):HT=0:HH=0
5070 GOSUB 3210:PRINT @302,"33%":GOSUB 1590:PRINT @302,"67%":GOSUB 1680:PRINT@302,"100%"
5080 GOTO 250
5090 CLEAR 500:CLS:PRINT@268,"LOADING"
5100 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30):HT=0:HH=0
5110 GOSUB 3210:GOSUB 1590
5120 CLS:PRINT@267,"CALCULATING":WW$=LEFT$(DA$,2):K$="MONTH"
5130 GOTO 3300
5140 CLEAR 500:CLS:PRINT@268,"LOADING"
5150 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(29):HT=0:HH=0
5160 GOSUB 3210:GOSUB 1680
5170 CLS:PRINT@267,"CALCULATING":K$="MONTH"
5180 GOTO 3900
5190 CLEAR 500:
5200 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5210 CLS:GOTO 3300
5220 CLEAR 500
5230 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5240 CLS:GOTO 3900
5250 REM CLS:GOTO 2070
5260 CLEAR 500:CLS:PRINT@268,"LOADING"
5270 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5280 CLS:PRINT@266,"CALCULATING":K$="year": GOTO 3300
5290 CLEAR 500:CLS:PRINT@268,"LOADING"
5300 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5310 GOSUB 3210:GOSUB 1590
5320 CLS:PRINT@267,"CALCULATING":K$="year": GOTO 3900
5330 CLEAR 500
5340 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5350 GOTO 1770
5360 CLEAR 500
5370 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5380 GOTO 4380
5390 CLEAR 500
5400 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5410 GOTO 740 
5420 CLEAR 500
5430 DIM MI(12):DIM MD(12):DIM CG$(30):DIM OT(30)
5440 GOTO 1210
5450 CLS:PRINT"CLEANING DATA":PRINT "THIS MAY TAKE SOME TIME":OPEN"D",#1,"IN.DAT",30
5455 IF LOF(1)<1 THEN CLOSE#1:GOTO 5540
5460 OPEN"D",#2,"TMP.DAT",30
5470 FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$
5475 FIELD#2,10 AS AD$,10 AS AT$,10 AS AA$
5480 A=LOF(1)
5490 FORE=1TOA
5500 GET#1,E
5510 IF VAL(CA$)<.01 THEN GOTO 5520
5511 LSET AD$=DD$:LSET AT$=TT$:LSET AA$=CA$
5512 PUT#2
5520 NEXT E
5530 CLOSE#1:CLOSE#2:KILL "IN.DAT":COPY "TMP.DAT"TO"IN.DAT"
5535 KILL "TMP.DAT"
5540 OPEN"D",#1,"OUT.DAT",30
5545 IF LOF(1)<1 THEN CLOSE#1:GOTO 5620
5550 OPEN"D",#2,"TMP.DAT",30
5560 FIELD#1,10 AS DD$,10 AS TD$,10 AS CD$
5565 FIELD#2,10 AS BD$,10 AS BC$,10 AS BE$
5570 B=LOF(1)
5580 FOR E=1TOB
5590 GET#1,E 
5600 IF VAL(CD$)<.01 THEN GOTO 5610
5605 LSET BD$=DD$:LSET BC$=TD$:LSET BE$=CD$:PUT#2
5610 NEXT E
5611 CLOSE#1:CLOSE#2:KILL "OUT.DAT":COPY "TMP.DAT"TO"OUT.DAT"
5615 KILL"TMP.DAT"
5620 OPEN"D",#1,"CAT.DAT",10
5625 IF LOF(1)<1THEN CLOSE#1:5930
5630 OPEN"D",#2,"TMP.DAT",10
5640 FIELD #1,10 AS CT$
5650 FIELD #2,10 AS ET$
5660 FORE=1TOLOF(1)
5670 GET#1,E 
5680 IF LEFT$(CT$,3)="   " THEN GOTO 5900
5890 LSET ET$=CT$:PUT#2
5900 NEXTE
5910 CLOSE#1:CLOSE#2:KILL "CAT.DAT":COPY "TMP.DAT"TO"CAT.DAT"
5920 KILL "TMP.DAT"
5930 RETURN
6000 END
0 
 